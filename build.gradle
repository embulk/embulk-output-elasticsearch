plugins {
    id "com.jfrog.bintray" version "1.1"
    id "com.github.jruby-gradle.base" version "0.1.5"
    id "java"
}
import com.github.jrubygradle.JRubyExec

allprojects {
    group = 'org.embulk.output.elasticsearch'
    version = "0.1.8"
}

subprojects {
    apply plugin: 'maven'  // install jar files to the local repo: $ gradle install
    apply plugin: 'java'
    apply plugin: 'com.github.jruby-gradle.base'

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    configurations {
        provided
    }

    dependencies {
        compile  "org.embulk:embulk-core:0.7.7"
        provided  "org.embulk:embulk-core:0.7.7"

        testCompile "junit:junit:4.+"
        testCompile "org.mockito:mockito-core:1.+"
    }

    task classpath(type: Copy, dependsOn: ["jar"]) {
        doFirst { file("classpath").deleteDir() }
        from (configurations.runtime - configurations.provided + files(jar.archivePath))
        into "classpath"
    }
    clean { delete 'classpath' }

    task gem(type: JRubyExec, dependsOn: ["build", "gemspec", "classpath"]) {
        jrubyArgs "-rrubygems/gem_runner", "-eGem::GemRunner.new.run(ARGV)", "build"
        script "${project.projectDir.absolutePath}/build/gemspec"
        doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "${parent.projectDir}/pkg") }
    }

    task gemspec << { file("build/gemspec").write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Muga Nishizawa"]
  spec.summary       = %[Elasticsearch output plugin for Embulk]
  spec.description   = %[Elasticsearch output plugin is an Embulk plugin that loads records to Elasticsearch read by any input plugins. Search the input plugins by "embulk-input" keyword.]
  spec.email         = ["muga.nishizawa@gmail.com"]
  spec.licenses      = ["Apache 2.0"]
  spec.homepage      = "https://github.com/muga/embulk-output-elasticsearch"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]
  spec.executables   = spec.files.grep(%r{^bin/}).map{ |f| File.basename(f) }
  spec.has_rdoc      = false

  spec.add_development_dependency "bundler", [">= 1.0"]
  spec.add_development_dependency "rake", [">= 10.0"]
  spec.add_development_dependency "test-unit", ["~> 3.0.2"]
end
/$)
    }
}

compileJava.options.encoding = 'UTF-8' // source encoding
sourceCompatibility = 1.7
targetCompatibility = 1.7

task gempush << {
    "gem push pkg/embulk-output-elasticsearch-${project.version}.gem".execute().waitFor()
    "gem push pkg/embulk-output-elasticsearch_1x-${project.version}.gem".execute().waitFor()
    "gem push pkg/embulk-output-elasticsearch_2x-${project.version}.gem".execute().waitFor()
}
